cbuffer MatrixBuffer : register(b0)
{
    matrix worldMatrix;
    matrix viewMatrix;
    matrix projectionMatrix;
	float4 lightPosition;
	float4 lightColor;
	float4 lightProperty; //(minimumDistance, minimumStrenght, _, _)
	float4 environmentColor;
};

Texture2D shaderTexture : register(t0);
SamplerState shaderSampler : register(s0);
TextureCube cubeMap : register(t1);

struct VIn
{
	float4 position : POSITION;
	float3 normal : NORMAL;
	float2 texcoord0 : TEXCOORD0;
};

struct PIn
{
    float4 position : SV_POSITION;
	float4 worldPosition : POSITION0;
	float3 worldNormal : NORMAL0;
	float4 viewPosition : POSITION1;
	float3 viewNormal : NORMAL1;
	float2 texcoord0 : TEXCOORD0;
};

PIn VShader(VIn input)
{
    PIn output;
    input.position.w = 1.0f;

    output.position = mul(input.position, worldMatrix);
	output.worldPosition = output.position;
    output.position = mul(output.position, viewMatrix);
	output.viewPosition = output.position;
    output.position = mul(output.position, projectionMatrix);
    
	output.worldNormal = mul(input.normal, (float3x3)worldMatrix);
	output.worldNormal = normalize(output.worldNormal);
	output.viewNormal = mul(output.worldNormal, (float3x3)viewMatrix);
	output.viewNormal = normalize(output.viewNormal);
    
    output.texcoord0 = input.texcoord0;
    
    return output;
}

float4 PShader(PIn input) : SV_TARGET
{
	float3 cubeMapNormal = reflect(input.viewPosition.xyz/input.viewPosition.w, input.viewNormal);
	float4 cubeMapColor = cubeMap.Sample(shaderSampler, cubeMapNormal);
	float4 textureColor = shaderTexture.Sample(shaderSampler, input.texcoord0);
	float4 materialColor = textureColor*0.4 + cubeMapColor*0.6;
	
	float4 lightDirection = lightPosition-input.worldPosition;
	float lightCos = max(0, dot(normalize(lightDirection), input.worldNormal));
	float lightDistance = length(lightDirection);
	float lightStrength = lightCos/max(1, (lightDistance*lightDistance)/lightProperty.x)*lightProperty.y;
	float4 diffuseColor = environmentColor+lightColor*float4(lightStrength,lightStrength,lightStrength,1);

	float4 color = materialColor*diffuseColor;
	return color;
}